program parallel_pi
  use mpi
  use iso_fortran_env, only : REAL64
  implicit none

  integer, parameter :: dp = REAL64

  integer, parameter :: n = 840
  integer :: myid, ntasks, rc

  integer :: status(MPI_STATUS_SIZE)

  integer :: i, istart, istop, chunksize, remainder
  real(dp) :: pi, localpi, x

  call mpi_init(rc)
  call mpi_comm_size(MPI_COMM_WORLD, ntasks, rc)
  call mpi_comm_rank(MPI_COMM_WORLD, myid, rc);

  if (myid == 0) then
     write(*,*) "Computing approximation to pi with n=", n
     write(*,*) "Using", ntasks, "mpi processes"
  end if

  chunksize = n / ntasks
  istart = myid * chunksize + 1
  istop = (myid + 1) * chunksize

  ! Handle possible uneven division
  remainder = mod(n, ntasks)

  if (remainder > 0) then
    if (myid < remainder) then
      ! Assign this task one element more
      istart = istart + myid
      istop = istop + myid + 1
    else
      istart = istart + remainder
      istop = istop + remainder
    end if
  end if

  localpi = 0.0
  do i = istart, istop
     x = (i - 0.5) / n
     localpi = localpi + SQRT(1. - x**2)/n  
  end do

  ! Reduce to rank 0
  CALL mpi_reduce(localpi,pi,1,MPI_DOUBLE_PRECISION, MPI_SUM, 0, MPI_COMM_WORLD, rc)


  if (myid == 0) then
     pi = pi * 4.0 
     write(*,'(A,F18.16,A,F10.8,A)') 'Approximate pi=', pi, ' (exact pi=', 4.0*atan(1.0_dp), ')'
  end if

  call mpi_finalize(rc)

end program

